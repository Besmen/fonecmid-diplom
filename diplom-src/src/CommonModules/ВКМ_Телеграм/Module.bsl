
#Область ПрограммныйИнтерфейс

Процедура ОтправитьУведомлениеВТелеграм() Экспорт
	
	ВсеСообщения = ПолучитьНакопленныеСообщения();
	
	Для Каждого Сообщение Из ВсеСообщения Цикл 
		
		Ответ = ОповеститьСпециалистов(Сообщение.ВКМ_ТекстСообщения);
		Если Ответ = 200 Тогда 
			Сообщение.Ссылка.ПолучитьОбъект().Удалить();
		КонецЕсли;
		
	КонецЦикла;
			
КонецПроцедуры

Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	
	Секрет = Запрос.ПараметрыURL.Получить("secret");
	
	Если Секрет <> Константы.ВКМ_СекретТелеграмБота.Получить() Тогда
		Ответ = Новый Структура;
		Ответ.Вставить("Result", "Error");
		Ответ.Вставить("ErrorText", "WrongSecret");
		Возврат ВКМ_ОбщегоНазначенияHTTP.ОтветJSON(Ответ, 403);
	КонецЕсли;	
	
	ДанныеЗапроса = ДанныеЗапроса(Запрос);
	
	Если ДанныеЗапроса.Свойство("message") Тогда
		Возврат ОбработатьСообщение(ДанныеЗапроса.message);
	КонецЕсли;
	
	Возврат ВКМ_ОбщегоНазначенияHTTP.ПростойУспешныйОтвет();
	
КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьНакопленныеСообщения();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_УведомленияТелеграмБоту.ВКМ_ТекстСообщения КАК ВКМ_ТекстСообщения,
	               |	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	Возврат Выгрузка;
	
КонецФункции

Функция ОповеститьСпециалистов(ТекстСообщения) Экспорт
	
	Токен = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	АдресРесурса = "/bot" + Токен + "/sendMessage"; 
	
    Соединение = Новый HTTPСоединение("api.telegram.org", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", ВКМ_ОбщегоНазначенияHTTP.ТипКонтентаJSON());
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	ИдентификаторЧата = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("chat_id", ИдентификаторЧата);
	ДанныеОтвета.Вставить("text", ТекстСообщения);
	ДанныеОтвета.Вставить("reply_markup", Клавиатура());
	
	Запрос.УстановитьТелоИзСтроки(ВКМ_ОбщегоНазначенияHTTP.СтрокаJSON(ДанныеОтвета));
	Результат = Соединение.ВызватьHTTPМетод("POST", Запрос); 
	
	Возврат Результат.КодСостояния;
	
КонецФункции

Функция ОбработатьСообщение(ДанныеСообщения) 
	
	Если ДанныеСообщения.Свойство("from") Тогда
		ЗафиксироватьОтправителя(ДанныеСообщения.from);
	КонецЕсли;
	
	ИдентификаторЧата = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	//ИдентификаторЧата = ДанныеСообщения.chat.id;
	
	ТекстСообщения = "Привет! Я Бот. Сейчас я занят. Напишите позже.";
	Если ДанныеСообщения.text = (Символ(55357) + Символ(56445)) Тогда 
		ТекстСообщения = "Займитесь делом.";
	ИначеЕсли ДанныеСообщения.text = "Взял в работу" Тогда 
		ТекстСообщения = "Молодец!";
	КонецЕсли;
	
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("method", "sendMessage");
	ДанныеОтвета.Вставить("chat_id", ИдентификаторЧата);
	ДанныеОтвета.Вставить("text", ТекстСообщения);
	ДанныеОтвета.Вставить("reply_markup", Клавиатура());
	
	Возврат ВКМ_ОбщегоНазначенияHTTP.ОтветJSON(ДанныеОтвета);	
	
КонецФункции

Функция Клавиатура()
	
	СтрокиКлавиатуры = Новый Массив;
	КнопкиСтрокиКлавиатуры = Новый Массив;
	
	КнопкаКлавиатуры = Новый Структура;
	КнопкаКлавиатуры.Вставить("text", "Взял в работу");
	КнопкиСтрокиКлавиатуры.Добавить(КнопкаКлавиатуры);
	
	КнопкаКлавиатуры = Новый Структура;
	КнопкаКлавиатуры.Вставить("text", Символ(55357) + Символ(56445));   

	КнопкиСтрокиКлавиатуры.Добавить(КнопкаКлавиатуры);
	
	СтрокиКлавиатуры.Добавить(КнопкиСтрокиКлавиатуры);
	
	ДанныеКлавиатуры = Новый Структура;
	ДанныеКлавиатуры.Вставить("keyboard", СтрокиКлавиатуры); 
	
	Возврат ДанныеКлавиатуры;
	
КонецФункции

Функция ДанныеЗапроса(Запрос)
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	Возврат ВКМ_ОбщегоНазначенияHTTP.ОбъектJSON(ТелоЗапроса);	
	
КонецФункции

Процедура ЗафиксироватьОтправителя(ДанныеОтправителя)
	
	Идентификатор = ДанныеОтправителя.id;
	
	УчетнаяЗапись = Справочники.ВКМ_УчетныеЗаписиТелеграм.НайтиПоКоду(Идентификатор);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиИмениПользователя = Новый Массив;
	
	Если ДанныеОтправителя.Свойство("first_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.first_name);
	КонецЕсли;
	
	Если ДанныеОтправителя.Свойство("last_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.last_name);
	КонецЕсли;
	
	Если ДанныеОтправителя.Свойство("username") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.username);
	КонецЕсли;        
	
	ИмяПользователя = СтрСоединить(ЧастиИмениПользователя, " ");
	
	СпрОбъект = Справочники.ВКМ_УчетныеЗаписиТелеграм.СоздатьЭлемент();
	СпрОбъект.Наименование = ИмяПользователя;
	СпрОбъект.Код = Идентификатор;
	СпрОбъект.Записать();
	
КонецПроцедуры


#КонецОбласти