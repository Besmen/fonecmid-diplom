
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина; 
	Движения.ВКМ_Удержания.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	СоздатьДвижения(); 
	
	ОкладРассчитан = РасчетОклада();
	
	Если Не ОкладРассчитан Тогда 
		ОбщегоНазначения.СообщитьПользователю("Проведение документа отменено");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РасчетОтпуска(); 
	РасчетПремии();
	РасчетНДФЛ();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьДвижения()

	//Регистр ВКМ_ОсновныеНачисления
	Для Каждого Строка Из ВКМ_ОсновныеНачисления Цикл 
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ВидРасчета = Строка.ВКМ_ВидРасчета;
		Движение.ПериодДействияНачало = Строка.ВКМ_ДатаНачала;
		Движение.ПериодДействияКонец = Строка.ВКМ_ДатаОкончания;
		Движение.ПериодРегистрации = Дата;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.ВКМ_ГрафикРаботы = Строка.ВКМ_ГрафикРаботы;
		
		Если Строка.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда 
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ВКМ_ДатаНачала, -12));
		    Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Строка.ВКМ_ДатаОкончания, -1));
			
		КонецЕсли;
				
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
	//Регистр ВКМ_ДополнительныеНачисления
	Для Каждого Строка Из ВКМ_ДополнительныеНачисления Цикл 
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = Строка.ВКМ_ВидРасчета;
		Движение.ПериодРегистрации = Дата;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
	  
	Данные = ДанныеДляУдержания();
	Для Каждого Строка Из Данные Цикл 
		
		//Регистр ВКМ_Удержания
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.ВКМ_Сотрудник = Строка.Сотрудник;
		Движение.ПериодРегистрации = Дата;
		
		//Регистр ВКМ_ВзаиморасчетыССотрудниками
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.ВКМ_Сотрудник = Строка.Сотрудник;
		
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры 

Функция РасчетОклада()
	
	ОкладРассчитан = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Оклад, 0) КАК Оклад,
	               |	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	               |	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК Сотрудник,
	               |	ВКМ_ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия КАК ОтработаноДней,
	               |	ВКМ_ОсновныеНачисленияДанныеГрафика.ДнейПериодДействия КАК ДнейПериодДействия
	               |ИЗ
	               |	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	               |			Регистратор = &Ссылка
	               |				И ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад)) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(
	               |				&Дата,
	               |				ВКМ_Сотрудник В
	               |					(ВЫБРАТЬ
	               |						ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	               |					ИЗ
	               |						Документ.ВКМ_НачисленияЗарплаты.ВКМ_ОсновныеНачисления КАК ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления
	               |					ГДЕ
	               |						ВКМ_НачисленияЗарплатыВКМ_ОсновныеНачисления.Ссылка = &Ссылка)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	               |		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Если Выборка.Оклад = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Для сотрудника %1 не установлен оклад", 
													Выборка.Сотрудник),
													,
													СтрШаблон("ВКМ_ОсновныеНачисления[%1].ВКМ_Сотрудник", XMLСтрока(Выборка.НомерСтроки - 1)));
													
			ОкладРассчитан = Ложь;
			Продолжить;
		КонецЕсли;
		
			
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		Движение.ВКМ_Показатель = Выборка.Оклад; 
		Движение.ВКМ_ОтработаноДней = Выборка.ОтработаноДней;  
		
		Если Выборка.ДнейПериодДействия = Выборка.ОтработаноДней Тогда 
			Движение.ВКМ_Результат = Выборка.Оклад;
		Иначе
			Движение.ВКМ_Результат = (Выборка.Оклад / Выборка.ДнейПериодДействия) * Выборка.ОтработаноДней;
		КонецЕсли;
				
	КонецЦикла;
	
	Если ОкладРассчитан Тогда 
		Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	КонецЕсли;
		
	Возврат ОкладРассчитан;
	
КонецФункции  

Процедура РасчетОтпуска()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	               |	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ВКМ_РезультатБаза, 0) КАК База,
	               |	РАЗНОСТЬДАТ(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияНачало, ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияКонец, ДЕНЬ) + 1 КАК ФактДней,
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ВКМ_ОтработаноДнейБаза КАК ОтработаноДнейБаза,
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ВКМ_Сотрудник КАК Сотрудник,
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
	               |	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияКонец КАК ПериодДействияКонец
	               |ИЗ
	               |	РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	               |			&Измерения,
	               |			&Измерения,
	               |			,
	               |			Регистратор = &Ссылка
	               |				И ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск)) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления";
	
	Измерения = Новый Массив;
	Измерения.Добавить("ВКМ_Сотрудник");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		Движение.ВКМ_Показатель = Выборка.База / Выборка.ОтработаноДнейБаза;
		Движение.ВКМ_Результат = Выборка.ФактДней * Движение.ВКМ_Показатель;
		Движение.ВКМ_ДнейОтпуска = (Выборка.ПериодДействияКонец + 1 - Выборка.ПериодДействияНачало) / (24 * 3600);
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);

КонецПроцедуры

Процедура РасчетПремии()
	
	ЕстьПроцент = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_ПроцентОтРабот КАК ПроцентОтРабот,
	               |	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_СуммаКОплатеОборот КАК СуммаКОплатеОборот,
	               |	ВКМ_ДополнительныеНачисления.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(
	               |			НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ),
	               |			КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	               |			Месяц,
	               |			ВКМ_Сотрудник В
	               |				(ВЫБРАТЬ
	               |					ВКМ_НачисленияЗарплатыВКМ_ДополнительныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	               |				ИЗ
	               |					Документ.ВКМ_НачисленияЗарплаты.ВКМ_ДополнительныеНачисления КАК ВКМ_НачисленияЗарплатыВКМ_ДополнительныеНачисления
	               |				ГДЕ
	               |					ВКМ_НачисленияЗарплатыВКМ_ДополнительныеНачисления.Ссылка = &Ссылка)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(
	               |				&Дата,
	               |				ВКМ_Сотрудник В
	               |					(ВЫБРАТЬ
	               |						ВКМ_НачисленияЗарплатыВКМ_ДополнительныеНачисления.ВКМ_Сотрудник КАК ВКМ_Сотрудник
	               |					ИЗ
	               |						Документ.ВКМ_НачисленияЗарплаты.ВКМ_ДополнительныеНачисления КАК ВКМ_НачисленияЗарплатыВКМ_ДополнительныеНачисления
	               |					ГДЕ
	               |						ВКМ_НачисленияЗарплатыВКМ_ДополнительныеНачисления.Ссылка = &Ссылка)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	               |		ПО ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	               |		ПО ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник = ВКМ_ДополнительныеНачисления.ВКМ_Сотрудник
	               |ГДЕ
	               |	ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
	               |	И ВКМ_ДополнительныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ДополнительныеНачисления.ПроцентОтРабот)"; 
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Движение = Движения.ВКМ_ДополнительныеНачисления[Выборка.НомерСтроки - 1];
		Движение.ВКМ_Процент = Выборка.ПроцентОтРабот;
		Движение.ВКМ_Результат = Выборка.СуммаКОплатеОборот;
		ЕстьПроцент = Истина;
		
	КонецЦикла;
	
	Если ЕстьПроцент Тогда 
		Движения.ВКМ_ДополнительныеНачисления.Записать(, Истина); 
	КонецЕсли;
		
КонецПроцедуры

Процедура РасчетНДФЛ()
	
	НДФЛУдержан = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	0=0,
	               |	СУММА(ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Результат) КАК РезультатОсн,
	               |	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник КАК Сотрудник,
				   |	ВКМ_ВзаиморасчетыССотрудниками.НомерСтроки КАК НомерСтрокиВзаиморасчеты,
				   |	ВКМ_ВзаиморасчетыССотрудниками.ВКМ_Сумма КАК Сумма,
	               |	ВКМ_Удержания.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
				   |		1=1 
				   |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками КАК ВКМ_ВзаиморасчетыССотрудниками
				   |		ПО ВКМ_Удержания.ВКМ_Сотрудник = ВКМ_ВзаиморасчетыССотрудниками.ВКМ_Сотрудник
				   |		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник = ВКМ_Удержания.ВКМ_Сотрудник
	               |ГДЕ
	               |	2=2
	               |	И ВКМ_Удержания.Регистратор = &Ссылка
				   |	И ВКМ_ВзаиморасчетыССотрудниками.Регистратор = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник,
	               |	ВКМ_Удержания.НомерСтроки,
				   |	ВКМ_ВзаиморасчетыССотрудниками.НомерСтроки,
				   |	ВКМ_ВзаиморасчетыССотрудниками.ВКМ_Сумма";
	
	Если ВКМ_ДополнительныеНачисления.Количество() > 0 Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "0=0,", "МАКСИМУМ(ВКМ_ДополнительныеНачисления.ВКМ_Результат) КАК РезультатДоп,");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "1=1", "ПОЛНОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	               									|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.ВКМ_Сотрудник = ВКМ_ДополнительныеНачисления.ВКМ_Сотрудник"); 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "2=2
	               									|	И ", "ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
	               									|	И ");
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "0=0,","");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "1=1", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "2=2
	               									|	И ", "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	НДФЛ = 13;
	
	Пока Выборка.Следующий() Цикл                               
		
		Если ВКМ_ДополнительныеНачисления.Количество() > 0 Тогда
			Показатель = Выборка.РезультатОсн + Выборка.РезультатДоп;
		Иначе 
			Показатель = Выборка.РезультатОсн;
		КонецЕсли;
		
		Удержание = Показатель * НДФЛ / 100;
		
		//Регистр ВКМ_Удержания
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		Движение.ВКМ_Показатель = Показатель;
		Движение.ВКМ_Результат = Удержание;  
		
		//Регистр ВКМ_ВзаиморасчетыССотрудниками
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками[Выборка.НомерСтрокиВзаиморасчеты - 1];
        Движение.ВКМ_Сумма = Показатель - Удержание;
		
		НДФЛУдержан = Истина;
		
	КонецЦикла;
	
	Если НДФЛУдержан Тогда 
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать(Истина);
		Движения.ВКМ_Удержания.Записать(, Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеДляУдержания()

	НаборЗаписейОсн = РегистрыРасчета.ВКМ_ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписейОсн.Отбор.Регистратор.Значение = Ссылка; 
	НаборЗаписейОсн.Прочитать();                         
	
	НаборЗаписейДоп = РегистрыРасчета.ВКМ_ДополнительныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписейДоп.Отбор.Регистратор.Значение = Ссылка;
	НаборЗаписейДоп.Прочитать();
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Сотрудник");
	ТЗ.Колонки.Добавить("Результат");
		
	Для Каждого Строка Из НаборЗаписейОсн Цикл 
		
		нСтрока = ТЗ.Добавить();
		нСтрока.Сотрудник = Строка.ВКМ_Сотрудник;
		нСтрока.Результат = Строка.ВКМ_Результат;
		
	КонецЦикла;  
	
	Для Каждого Строка Из НаборЗаписейДоп Цикл 
		
		нСтрока = ТЗ.Добавить();
		нСтрока.Сотрудник = Строка.ВКМ_Сотрудник;
		нСтрока.Результат = Строка.ВКМ_Результат;
		
	КонецЦикла;
	
	ТЗ.Свернуть("Сотрудник", "Результат");
	
	Возврат ТЗ;

КонецФункции

//Процедура ДобавитьСтрокуУдержания()
//
//	ОбъектДок = Ссылка.ПолучитьОбъект(); 
//	ОбъектДок.ВКМ_Удержания.Очистить();
//	Данные = ДанныеДляУдержания();
//	
//	Для Каждого СтрокаСотрудник Из Данные Цикл 
//		
//		Строка = ОбъектДок.ВКМ_Удержания.Добавить();
//		Строка.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
//		Строка.ВКМ_ДатаНачала = НачалоМесяца(Дата);
//		Строка.ВКМ_ДатаОкончания = КонецМесяца(Дата);
//		Строка.ВКМ_Сотрудник = СтрокаСотрудник.Сотрудник;
//		ОбъектДок.Записать();
//		
//	КонецЦикла;
//
//КонецПроцедуры


#КонецОбласти

#КонецЕсли
